buildscript {
    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/gesellix/gradle-plugins' }
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow',                 version: '1.2.3'
        classpath group: 'de.gesellix',                         name: 'gradle-debian-plugin',   version: '14'
        classpath group: 'org.springframework.build.gradle',    name: 'propdeps-plugin',        version: '0.0.7'
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release'        version '1.2.3'
    id 'com.ewerk.gradle.plugins.integration-test'  version '1.0.8'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'jacoco'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'propdeps'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'
apply plugin: 'pkg-debian'

/***** basics *****/

project.group = 'pl.allegro.tech.hadoop'
scmVersion {
    tag {
        prefix = project.rootProject.name
    }
}
project.version = scmVersion.version

mainClassName = 'pl.allegro.tech.hadoop.compressor.Compressor'
sourceCompatibility = JavaVersion.VERSION_1_7

/***** dependencies *****/

repositories {
    mavenCentral()
    maven { url 'http://typesafe.artifactoryonline.com/typesafe/twitter-cache' }
    maven { url 'https://repository.cloudera.com/artifactory/cloudera-repos/' }
    maven { url 'http://maven.twttr.com' }
}

configurations {
    all*.exclude group: 'javax.servlet'
    provided.exclude module: 'hadoop-client'
    integrationCompile.extendsFrom(testCompile)
}

dependencies {
    provided group: 'org.apache.spark',     name: 'spark-core_2.10',    version: '1.6.1'
    provided group: 'org.scala-lang',       name: 'scala-library',      version: '2.10.2'
    provided group: 'org.apache.hadoop',    name: 'hadoop-hdfs',        version: '2.6.0'
    provided group: 'org.apache.hadoop',    name: 'hadoop-common',      version: '2.6.0'

    provided 'org.apache.httpcomponents:httpclient:4.5'

    compile group: 'org.apache.hadoop',         name: 'hadoop-mapreduce-client-core',   version: '2.6.0'
    compile group: 'org.schemarepo',            name: 'schema-repo-client',             version: '0.1.3'
    compile group: 'com.hadoop.gplcompression', name: 'hadoop-lzo',                     version: '0.4.19'

    compile group: 'org.apache.kafka',          name: 'kafka_2.10',     version: '0.8.2.2'

    testCompile group: 'org.mockito',       name: 'mockito-all',        version: '1.9.5'
    testCompile group: 'junit',             name: 'junit',              version: '4.11'
    testCompile group: 'org.apache.hadoop', name: 'hadoop-hdfs',        version: '2.6.0', classifier: 'tests'
    testCompile group: 'org.apache.hadoop', name: 'hadoop-common',      version: '2.6.0', classifier: 'tests'

    testCompile (group: 'com.palantir',      name: 'curator-test-rule',  version: '0.0.3') {
        exclude group: 'com.google.guava'
    }

    testCompile (group: 'com.github.tomakehurst', name: 'wiremock', version: '1.58') {
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.google.guava'
    }
}



idea {
    module {
        scopes.TEST.plus += [ configurations.integrationCompile ]
        testSourceDirs += file("${projectDir}/src/integration/resources")
    }
}

/***** packaging *****/

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    dependencies {
        exclude(dependency('org.apache.spark:spark-core_2.10:1.6.1'))
        exclude(dependency('org.scala-lang:scala-library:2.10.2'))
        exclude(dependency('org.apache.hadoop:hadoop-hdfs:2.6.0'))
    }
}

task prepareControlFile(type: Copy) {
  from 'src/package'
  into 'build/package'
  include 'control.tpl'
  rename { file -> 'control' }
  expand(project: project)
}

debian {
  packagename = 'camus-compressor'
  controlDirectory = 'build/package'
  changelogFile = 'src/package/changelog'

  data {
    def baseDir = 'build'
    file {
      name = "${baseDir}/libs/camus-compressor-${project.version}-all.jar"
      target = "usr/lib/hadoop-tools/camus-compressor/compressor-${project.version}.jar"
      mapper {
        fileMode = '644'
      }
    }
    file {
      name = 'src/main/resources/camus-compressor.properties'
      target = 'etc/camus-compressor/camus-compressor.properties'
      mapper {
        fileMode = '644'
      }
    }
    file {
      name = 'src/main/resources/compressor.sh'
      target = 'usr/lib/hadoop-tools/camus-compressor/compressor.sh'
      mapper {
        fileMode = '755'
      }
    }
    link {
      path = 'usr/lib/hadoop-tools/camus-compressor/compressor.jar'
      name = "/usr/lib/hadoop-tools/camus-compressor/compressor-${project.version}.jar"
    }
  }
}

integrationTest {
    jvmArgs "-XX:MaxPermSize=256m"
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
